1. GPath支持
```groovy
	// 由于支持List和Map的属性表示法,Groovy提供了语法糖,使得处理嵌套集合变得非常容易,如以下示例所示：
	def listOfMaps = [['a': 11, 'b': 12], ['a': 21, 'b': 22]]
	assert listOfMaps.a == [11, 21] //GPath notation
	assert listOfMaps*.a == [11, 21] //spread dot notation

	listOfMaps = [['a': 11, 'b': 12], ['a': 21, 'b': 22], null]
	assert listOfMaps*.a == [11, 21, null] // caters for null values
	assert listOfMaps*.a == listOfMaps.collect { it?.a } //equivalent notation
	// But this will only collect non-null values
	assert listOfMaps.a == [11,21]
```
2. * 操作符
```groovy
	// 解构
	assert [ 'z': 900,
	         *: ['a': 100, 'b': 200], 'a': 300] == ['a': 300, 'b': 200, 'z': 900]
	//spread map notation in map definition
	assert [*: [3: 3, *: [5: 5]], 7: 7] == [3: 3, 5: 5, 7: 7]

	def f = { [1: 'u', 2: 'v', 3: 'w'] }
	assert [*: f(), 10: 'zz'] == [1: 'u', 10: 'zz', 2: 'v', 3: 'w']
	//spread map notation in function arguments
	f = { map -> map.c }
	assert f(*: ['a': 10, 'b': 20, 'c': 30], 'e': 50) == 30

	f = { m, i, j, k -> [m, i, j, k] }
	//using spread map notation with mixed unnamed and named arguments
	assert f('e': 100, *[4, 5], *: ['a': 10, 'b': 20, 'c': 30], 6) ==
	        [["e": 100, "b": 20, "c": 30, "a": 10], 4, 5, 6]
```
3. \*\. 操作符
```groovy
	assert [1, 3, 5] == ['a', 'few', 'words']*.size()

	class Person {
	    String name
	    int age
	}
	def persons = [new Person(name:'Hugo', age:17), new Person(name:'Sandra',age:19)]
	assert [17, 19] == persons*.age
```
4. 用下标运算符做切片
```groovy
	// 您可以使用下标表达式索引列表,数组和映射.有趣的是,字符串在该上下文中被视为特殊类型的集合
	def text = 'nice cheese gromit!'
	def x = text[2]

	assert x == 'c'
	assert x.class == String

	def sub = text[5..10]
	assert sub == 'cheese'

	def list = [10, 11, 12, 13]
	def answer = list[2,3]
	assert answer == [12,13]
	// 请注意,您可以使用Range来提取集合的一部分
	list = 100..200
	sub = list[1, 3, 20..25, 33]
	assert sub == [101, 103, 120, 121, 122, 123, 124, 125, 133]
	// 使用下标运算符更新集合
	list = ['a','x','x','d']
	list[1..2] = ['b','c']
	assert list == ['a','b','c','d']
	// 值得注意的是,允许使用负索引,从集合的末尾提取
	text = "nice cheese gromit!"
	x = text[-1]
	assert x == "!"
	def name = text[-7..-2]
	assert name == "gromit"
	// 如果您使用向后范围(起始索引大于结束索引)
	text = "nice cheese gromit!"
	name = text[3..1]
	assert name == "eci"
```
