1. 没有分号
 > 分号不是必须的
2. return关键字可选
 > 可以返回在方法体中计算的最后一个表达式，而无需return
 ```groovy
 	String toString() { return "a server" }
	String toString() { "a server" }
 ```
 > 当使用变量时，可读性较差
 ```groovy
 	// 可读性较差，并且在通常情况下会声明返回类型或者指定为void
 	def props() {
	    def m1 = [a: 1, b: 2]
	    m2 = m1.findAll { k, v -> v % 2 == 0 }
	    m2.c = 3
	    m2
	}
	// 但是在闭包中通常是省略的
 ```
 > 对于if/else,try/catch同样可以省略return
 ```groovy
 	def foo(n) {
	    if(n == 1) {
	        "Roshan"
	    } else {
	        "Dawrani"
	    }
	}

	assert foo(1) == "Roshan"
	assert foo(2) == "Dawrani"
 ```
3. def还是明确指定类型
 > 当类型明确是不要使用def
 ```groovy
 	// not recommend
 	def String name = "Guillaume"
 	// recommend
 	String name = "Guillaume"
 ```
 > 在Groovy中使用def时,实际的类型持有者是Object(因此您可以将任何对象分配给使用def定义的变量,并在声明返回def的方法时返回任何类型的对象),但是最佳实践是明确参数类型
 ```groovy
 	// 当定义一个参数类型不确定的方法时,def不是必须的，是可省略的
 	// not recommend
 	void doSomething(def param1, def param2) { }
 	// recommend
 	void doSomething(param1, param2) { }
 	// best practices
 	void doSomething(String param1, String param2) { }
 ```
 > 在使用构造函数时应避免使用def
 ```groovy
 	// not recommend
 	class MyClass {
	    def MyClass() {}
	}
	// recommend
	class MyClass {
	    MyClass() {}
	}
 ```
4. 默认public
 > 默认情况下，Groovy将类和方法视为public。所以你不必在任何地方公开使用public修饰符。只有当它不公开时，才应该设置可见性修饰符。\
 ```groovy
  	// not recommend
  	public class Server {
  	  public String toString() { return "a server" }
	}
	// recommend
	class Server {
	    String toString() { "a server" }
	}
 ```
  > 您可能想知道'package-scope'可见性，而Groovy允许省略'public'这一事实意味着默认情况下不支持此范围，但实际上有一个特殊的Groovy注释允许您使用该可见性
 ```groovy
  	class Server {
	    @PackageScope Cluster cluster
	}
 ```
5. 省略括号
 > Groovy允许省略括号
 ```groovy
 	// recommend
 	println "Hello"
	method a, b
	// not recommend
	println("Hello")
	method(a, b)
 ```
 > 当方法中的最后一个参数是闭包时，例如Groovy的each方法，可以把闭包放到括号外，甚至省略括号
 ```groovy
 	list.each( { println it } )
	list.each(){ println it }
	// 建议使用，更自然，空括号只是无用的语法噪音
	list.each  { println it }
 ```
 > 某些情况下括号是必须的，例如调用自定义方法或者调用无参的自定义方法
 ```groovy
 	def foo(n) { n }
	def bar() { 1 }

	println foo 1 // won't work
	def m = bar   // won't work
 ```
6. Classes作为首选项
 > .class后缀在Groovy中不是必须的
 ```groovy
 	// not recommend
 	connection.doPost(BASE_URI + "/modify.hqu", params, ResourcesResponse.class)
 	// recommend
 	connection.doPost("${BASE_URI}/modify.hqu", params, ResourcesResponse)
 ```
7. Getters和Setters
 > 在Groovy中,getter和setter形成我们称之为"属性"的东西,并提供用于访问和设置此类属性的快捷方式表示法,因此,您可以使用类似字段的访问符号来代替调用getter/setter的Java方式
 ```groovy
 	// not recommend
 	resourceGroup.getResourcePrototype().getName() == SERVER_TYPE_NAME
	// recommend
	resourceGroup.resourcePrototype.name == SERVER_TYPE_NAME
	// not recommend
	resourcePrototype.setName("something")
	// recommend
	resourcePrototype.name = "something"
 ```
 > 在Groovy中编写bean时,通常称为POGO(Plain Old Groovy Objects),您不必自己创建字段和getter/setter,而是让Groovy编译器为您完成
 ```groovy
 	// not recommend
 	class Person {
	    private String name
	    String getName() { return name }
	    void setName(String name) { this.name = name }
	}
	// recommend
	class Person {
	    String name
	}
 ```
 > 当需要在getters/setters中执行其他操作可以显示提供，编译器会使用自定义的方法而不是使用默认生成的
8. 使用命名参数和默认构造函数初始化bean
 ```groovy
 	class Server {
	   	String name
	    Cluster cluster
	}
	def server = new Server()
	server.name = "Obelix"
	server.cluster = aCluster
	// 您可以使用默认构造函数使用命名参数(首先调用构造函数,然后按照在映射中指定它们的顺序调用setter)
	def server = new Server(name: "Obelix", cluster: aCluster)
 ```
9. 使用with()和tap()对同一个bean进行重复操作
 > 使用默认构造函数的命名参数在创建新实例时很方便,但是如果要更新提供给您的实例,您是否必须一次又一次地重复前缀？Groovy的with()和tap()方法能够解决该问题
 ```groovy
 	// not recommend
 	server.name = application.name
	server.status = status
	server.sessionCount = 3
	server.start()
	server.stop()
	// recommend
	server.with {
	    name = application.name
	    status = status
	    sessionCount = 3
	    start()
	    stop()
	}
 ```
 > 在groovy中闭包的最后一条语句被认为是返回值，为了返回对象必须显示指定it为其返回
 ```groovy
 	// not recommend
 	def person = new Person().with {
	    name = "Ada Lovelace"
	    it // Note the explicit mention of it as the return value
	}
	// recommend
	def person = new Person().tap {
	    name = "Ada Lovelace"
	}
 ```
 > 可以使用with(true)代替tap(),使用with(false)代替with()
10. Equals和==
 > Java中的==等价于Groovy的is()方法, Groovy的==等价于Java中的equals(),为了比较引用应该使用a.is(b)
 > 但是为了进行通常的equals()比较,你应该更喜欢Groovy的==,因为它也会注意避免NullPointerException,而不管左边或右边是否为null。
 ```groovy
 	// not recommend
 	status != null && status.equals(ControlConstants.STATUS_COMPLETED)
 	// recommend
 	status == ControlConstants.STATUS_COMPLETED
 ```
 11. GString(插值和多行)
 > 我们经常在Java中使用字符串和变量连接,其中许多打开/关闭双引号,加号,以及换行符的\n字符。使用插值字符串(称为GStrings),这样的字符串看起来更好,输入的痛苦也更少 
 ```groovy
  	// not recommend
  	throw new Exception("Unable to convert resource: " + resource)
  	// recommend
  	throw new Exception("Unable to convert resource: ${resource}")
  	// 在花括号内,你可以放置任何类型的表达式,而不仅仅是变量。对于简单变量或variable.property,您甚至可以删除花括号
  	throw new Exception("Unable to convert resource: $resource")
  	// 甚至可以使用带有$ { - > resource}的闭包表示法来懒加载这些表达式,当GString被强制转换为String时,它将加载闭包并获取返回值的toString()
  	int i = 3
  	// 此时s1的值已经确定
	def s1 = "i's value is: ${i}"
	def s2 = "i's value is: ${-> i}"

	i++

	assert s1 == "i's value is: 3" // eagerly evaluated, takes the value on creation
	assert s2 == "i's value is: 4" // lazily evaluated, takes the new value into account
 ```
 > \和三引号
 ```groovy
  	// not recommend
  	throw new PluginException("Failed to execute command list-applications:" +
	    " The group with name " +
	    parameterMap.groupname[0] +
	    " is not compatible group of type " +
	    SERVER_TYPE_NAME)
	// not recommend
	throw new PluginException("Failed to execute command list-applications: \
		The group with name ${parameterMap.groupname[0]} \
		is not compatible group of type ${SERVER_TYPE_NAME}")
	// recommend
	throw new PluginException("""Failed to execute command list-applications:
	    The group with name ${parameterMap.groupname[0]}
	    is not compatible group of type ${SERVER_TYPE_NAME)}""")
	// 还可以通过在该字符串上调用.stripIndent()来删除出现在多行字符串左侧的缩进
 ```
 > 还要注意Groovy中单引号和双引号之间的区别:单引号始终创建Java字符串,不插入变量,而双引号在存在插值变量时创建Java字符串或GStrings。
 > 对于多行字符串,您可以将引号加倍:即GStrings的三重双引号和仅仅字符串的三重单引号\
 > 如果需要编写正则表达式模式,则应使用"slashy"字符串表示法,"slashy"符号的优点是你不需要双重转义反斜杠,使得使用正则表达式更简单一点
 ```groovy
  	assert "foooo/baaaaar" ==~ /fo+\/ba+r/
 ```
 > 对于字符串常量建议使用单引号
12. 数据结构的本地语法
 > Groovy为列表,映射,正则表达式或值范围等数据结构提供本机语法结构
 ```groovy
 	def list = [1, 4, 6, 9]

	// by default, keys are Strings, no need to quote them
	// you can wrap keys with () like [(variableStateAcronym): stateName] to insert a variable or object as a key.
	def map = [CA: 'California', MI: 'Michigan']

	// ranges can be inclusive and exclusive
	def range = 10..20 // inclusive
	assert range.size() == 11
	// use brackets if you need to call a method on a range definition
	assert (10..<20).size() == 10 // exclusive

	def pattern = ~/fo*/

	// equivalent to add()
	list << 5

	// call contains()
	assert 4 in list
	assert 5 in list
	assert 15 in range

	// subscript notation
	assert list[1] == 4

	// add a new key value pair
	map << [WA: 'Washington']
	// subscript notation
	assert map['CA'] == 'California'
	// property notation
	assert map.WA == 'Washington'

	// matches() strings against patterns
	assert 'foo' ==~ pattern
 ```
13. 强大的switch
 ```groovy
 	def x = 1.23
	def result = ""
	switch (x) {
	    case "foo": result = "found foo"
	    // lets fall through
	    case "bar": result += "bar"
	    case [4, 5, 6, 'inList']:
	        result = "list"
	        break
	    case 12..30:
	        result = "range"
	        break
	    case Integer:
	        result = "integer"
	        break
	    case Number:
	        result = "number"
	        break
	    case { it > 3 }:
	        result = "number > 3"
	        break
	    default: result = "default"
	}
	assert result == "number"
 ```
14. 导包方言
 ```groovy
 	// 导包
 	import java.util.List as UtilList
	import java.awt.List as AwtList
	import javax.swing.WindowConstants as WC

	UtilList list1 = [WC.EXIT_ON_CLOSE]
	assert list1.size() instanceof Integer
	def list2 = new AwtList()
	assert list2.size() instanceof java.awt.Dimension
	// 导入方法
	import static java.lang.Math.abs as mabs
	assert mabs(-4) == 4
 ```
15. Groovy中的布尔值
 > 可以将所有对象"强制"为布尔值:null,void,等于零或empty的所有值的计算结果为false,如果不是,则计算结果为true
 ```groovy
 	// not recommend
 	if (name != null && name.length > 0) {}
 	// recommend
 	if (name) {}
 	// 甚至可以通过向类添加布尔值asBoolean()方法来自定义Groovy Truth！
 ```
16. 安全导航
 > 在整个调用链中检查空值,如果任何元素为null,则不会抛出NullPointerException,如果某些元素为null,则结果值为null
 ```groovy
 	// not recommend
 	if (order != null) {
	    if (order.getCustomer() != null) {
	        if (order.getCustomer().getAddress() != null) {
	            System.out.println(order.getCustomer().getAddress());
	        }
	    }
	}
	// recommend
	println order?.customer?.address
 ```
17. 断言
 ```groovy
 	def check(String name) {
	    // name non-null and non-empty according to Groovy Truth
	    assert name
	    // safe navigation + Groovy Truth to check
	    assert name?.size() > 3
	}
 ```
18. Elvis运算符用于默认值
 ```groovy
 	// not recommend
 	def result = name != null ? name : "Unknown"
 	// recommend
 	def result = name ?: "UNknown"
 ```