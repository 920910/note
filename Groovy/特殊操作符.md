1. ?:操作符
 > ?:操作符是三目运算符的特殊形式。当一个对象的属性不存在是给其赋予默认值
 ```groovy
 displayName = user.name ?: 'jack'
 ```
2. ?.操作符
 > 该操作符用于避免空指针异常 
 ```groovy
 class Person {
    Integer id
    String name
}

def person = new Person().find { it.id == 123 }
String name = person?.name
assert name == null
 ```
3. .和.@操作符
 > .操作符相当于调用类的getter方法，.@相当于直接访问属性
 ```groovy
 class User {
     public final String name
     User(String name) { this.name = name}
     String getName() { "Name: $name" }
 }
def user = new User('Bob')
assert user.name == 'Name: Bob'
assert user.@name == 'Bob'
 ```
4. .&操作符
 > 方法指针运算符(.&)调用用于存储对变量中方法的引用,以便稍后调用它
 ```groovy
 // 定义一个字符串并赋值
 def str = 'example of method reference'            
 // 将toUpperCase方法的引用绑定到字符串上
def fun = str.&toUpperCase            
// 调用方法             
def upper = fun()                                  
assert upper == str.toUpperCase()   
 ```
 > 使用的先决条件是该方法类型是groovy.lang.Closure
 > 它适合转换现有方法以满足策略模式的需要
 ```groovy
	class Person {
	    String name
	    Integer age
	}
 	def transform(List elements, Closure action) {
	    def result = []
	    elements.each {
	        result << action(it)
	    }
	    result
	}
	String describe(Person p) {
	    "$p.name is $p.age"
	}
	def action = this.&describe
	def list = [
	        new Person(name: 'Bob',   age: 42),
	        new Person(name: 'Julia', age: 35)]
	assert transform(list, action) == ['Bob is 42', 'Julia is 35']
 ```
 > 支持重载
 ```groovy
 	def doSomething(String str) { str.toUpperCase() }    
	def doSomething(Integer x) { 2*x }                   
	def reference = this.&doSomething                    
	assert reference('foo') == 'FOO'                     
	assert reference(123)   == 246  
 ```
5. ~操作符
 > 创建一个java.util.regex.Pattern实例
 ```groovy
 	def p = ~/foo/
	assert p instanceof Pattern
	// 虽然通常情况下,~操作符的表达式是斜线加字符串,但它可以用于Groovy中的任何类型的字符串
	p = ~'foo'                                                        
	p = ~"foo"      
	// 使用$//$使得$和/无需转义                                                  
	p = ~$/dollar/slashy $ string/$                                   
	p = ~"${pattern}" 
 ```
6. =~操作符
 > 创建一个java.util.regex.Matcher实例
 ```groovy
 	def text = "some text to match"
 	// =~使用右侧的模式为文本变量创建匹配器
	def m = text =~ /match/
	assert m instanceof Matcher
	// 等同于if (!m.find())
	if (!m) {
	    throw new RuntimeException("Oops, text not found!")
	}

 ```
7. ==~操作符
 > 类似=~，但是不是返回Matcher而是返回boolean
 ```groovy
 	def text = "some text to match"
 	m = text ==~ /match/                                              
	assert m instanceof Boolean   
	// 等同于if (text ==~ /match/)                                    
	if (m) {                                                          
	    throw new RuntimeException("Should not reach that point!")
	}
 ```
8. \*. 操作符
 > 用于聚合对象的所有元素上的操作。它相当于对每个元素调用操作并将结果收集到列表中
 ```groovy
 	class Car {
	    String make
	    String model
	}
	def cars = [
	        new Car(make: 'Peugeot', model: '508'),
	        new Car(make: 'Renault', model: 'Clio')]
	// 等同于cars.collect{ it.make }	 
	// 可简写成def makes = cars.make       
	def makes = cars*.make
	assert makes == ['Peugeot', 'Renault']

 ```
 > 该运算符是null安全的,这意味着如果集合的元素为null,它将返回null而不是抛出NullPointerException
 ```groovy
 	class Car {
	    String make
	    String model
	}
 	cars = [
	   new Car(make: 'Peugeot', model: '508'),
	   null,                                              
	   new Car(make: 'Renault', model: 'Clio')]
	assert cars*.make == ['Peugeot', null, 'Renault']     
	assert null*.make == null
 ```
 > 可以用于任意实现了Iterable接口的类
 ```groovy
 	class Component {
	    Long id
	    String name
	}
	class CompositeObject implements Iterable<Component> {
	    def components = [
	        new Component(id: 1, name: 'Foo'),
	        new Component(id: 2, name: 'Bar')]

	    @Override
	    Iterator<Component> iterator() {
	        components.iterator()
	    }
	}
	def composite = new CompositeObject()
	assert composite*.id == [1,2]
	assert composite*.name == ['Foo','Bar']
 ```
 > 在处理本身包含聚合的数据结构聚合时,使用扩展点运算符的多次调用
 ```groovy
 	class Make {
	    String name
	    List<Model> models
	}

	@Canonical
	class Model {
	    String name
	}

	def cars = [
	    new Make(name: 'Peugeot',
	             models: [new Model('408'), new Model('508')]),
	    new Make(name: 'Renault',
	             models: [new Model('Clio'), new Model('Captur')])
	]

	def makes = cars*.name
	assert makes == ['Peugeot', 'Renault']
	// 等同于cars.collectNested{ it.model }
	def models = cars*.models*.name
	assert models == [['408', '508'], ['Clio', 'Captur']]
	assert models.sum() == ['408', '508', 'Clio', 'Captur'] // flatten one level
	assert models.flatten() == ['408', '508', 'Clio', 'Captur'] // flatten all levels (one in this case)
 ```
 > 用于方法参数
 ```groovy
 	int function(int x, int y, int z) {
	    x*y+z
	}
	def args = [4,5,6]
	// idea可能报错，不影响运行
	assert function(*args) == 26
	// 混合使用也能正常运行
	args = [4]
	assert function(*args,5,6) == 26
 ```
 > 用于list
 ```groovy
 	def items = [4,5]                      
	def list = [1,2,3,*items,6]            
	assert list == [1,2,3,4,5,6]
 ```
 > 用于map
 ```groovy
 	def m1 = [c:3, d:4]                   
	def map = [a:1, b:2, *:m1]            
	assert map == [a:1, b:2, c:3, d:4]
	// 注意位置
	def m1 = [c:3, d:4]                   
	def map = [a:1, b:2, *:m1, d: 8]      
	assert map == [a:1, b:2, c:3, d:8] 
 ```
9. ..操作符
 > 是闭合区间，..<是左闭合
 ```groovy
 	def range = 0..5                                    
	assert (0..5).collect() == [0, 1, 2, 3, 4, 5]       
	assert (0..<5).collect() == [0, 1, 2, 3, 4]         
	assert (0..5) instanceof List                       
	assert (0..5).size() == 6   
	assert ('a'..'d').collect() == ['a','b','c','d']
 ```
10. <==>操作符
 > 是compareTo方法的委托
 ```groovy
 	assert (1 <=> 1) == 0
	assert (1 <=> 2) == -1
	assert (2 <=> 1) == 1
	assert ('a' <=> 'z') == -1
 ```
11. []操作符
 > 是getAt或者putAt的简写
 ```groovy
 	def list = [0,1,2,3,4]
 	// getAt(2)
	assert list[2] == 2   
	// putAt(2)                      
	list[2] = 4   
	// getAt支持范围                              
	assert list[0..2] == [0,1,4]                
	// putAt支持范围
	list[0..2] = [6,6,6]                        
	assert list == [6,6,6,3,4] 
 ```
 > 下标运算符与getAt/putAt的自定义实现相结合是解构对象的便捷方法
 ```groovy
 	class User {
	    Long id
	    String name
	    def getAt(int i) {
	        switch (i) {
	            case 0: return id
	            case 1: return name
	        }
	        throw new IllegalArgumentException("No such element $i")
	    }
	    void putAt(int i, def value) {
	        switch (i) {
	            case 0: id = value; return
	            case 1: name = value; return
	        }
	        throw new IllegalArgumentException("No such element $i")
	    }
	}
	def user = new User(id: 1, name: 'Alex')
	assert user[0] == 1
	assert user[1] == 'Alex'
	user[1] = 'Bob'
	assert user.name == 'Bob'
 ```
12. in操作符
 > 等同于isCase方法，在list中等同contains方法
 ```groovy
 	def list = ['Grace','Rob','Emmy']
 	// 等同于list.contains('Emmy') or list.isCase('Emmy')
	assert ('Emmy' in list) 
 ```
13. ==和is
 > ==比较相等性，is比较引用
 ```groovy
 	def list1 = ['Groovy 1.8','Groovy 2.0','Groovy 2.3']        
	def list2 = ['Groovy 1.8','Groovy 2.0','Groovy 2.3']        
	assert list1 == list2                                       
	assert !list1.is(list2) 
 ```
14. as操作符
 > 类型转换,当一个对象被强制转换为另一个对象时,除非目标类型与源类型相同,否则强制将返回一个新对象.强制规则因源和目标类型而异,如果没有找到转换规则,强制可能会失败。 由于asType方法,可以实现自定义转换规则
 ```groovy
 	class Identifiable {
	    String name
	}
	class User {
	    Long id
	    String name
	    def asType(Class target) {
	        if (target == Identifiable) {
	            return new Identifiable(name: name)
	        }
	        throw new ClassCastException("User cannot be coerced into $target")
	    }
	}
	def u = new User(name: 'Xavier')
	def p = u as Identifiable
	assert p instanceof Identifiable
	assert !(p instanceof User)
 ```
15. ()操作符
 > ()用于调用方法名为call的方法
 ```groovy
 	class MyCallable {
	    int call(int x, int y) {
	        2*x + y
	    }
	}

	def mc = new MyCallable()
	assert mc.call(2, 2) == 6
	assert mc(2, 2) == 6
 ```
